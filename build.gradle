plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.sinri'
version = '5.0.0-rc.21'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()

    modularity.inferModulePath.set(true)
}

repositories {
    maven {
        url 'https://nexus.leqeegroup.com/repository/public/'
        credentials {
            username = project.findProperty("leqeeNexusUsername")
            password = project.findProperty("leqeeNexusPassword")
        }
    }
    maven {
        url 'https://nexus.leqeegroup.com/repository/releases/'
        credentials {
            username = project.findProperty("leqeeNexusUsername")
            password = project.findProperty("leqeeNexusPassword")
        }
    }
    maven {
        url 'https://nexus.leqeegroup.com/repository/snapshots/'
        credentials {
            username = project.findProperty("leqeeNexusUsername")
            password = project.findProperty("leqeeNexusPassword")
        }
    }
    mavenCentral()
}

dependencies {
    def vertxVersion = '5.0.6'
    def keelCoreVersion = '5.0.0-rc.22'
    def keelTestVersion = '5.0.0-rc.20'

    compileOnly "io.github.sinri:keel-core:${keelCoreVersion}"
    testImplementation "io.github.sinri:keel-test:${keelTestVersion}"

    api "io.vertx:vertx-web:${vertxVersion}"

    // https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-models
    implementation 'io.swagger.core.v3:swagger-models:2.2.41'

    // https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-core
    testImplementation 'io.swagger.core.v3:swagger-core:2.2.41'

    testImplementation 'io.github.sinri:keel-logger-ext:5.0.0-rc.14'
    testImplementation 'org.slf4j:slf4j-api:2.0.17'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

processResources {
    exclude 'config.properties'
}

tasks.named('test') {
    useJUnitPlatform()
    include 'io/github/sinri/keel/base/**/*UnitTest.class'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Keel-Web'
                description = 'The component of Keel provides Web Functions.'
                url = 'https://github.com/sinri/keel-web'
                licenses {
                    license {
                        name = 'GPL-v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Sinri Edogawa'
                        email = 'e.joshua.s.e@gmail.com'
                        organization = 'Sinri Edogawa'
                        organizationUrl = 'https://sinri.github.io'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sinri/kele-web.git'
                    developerConnection = 'scm:git:https://github.com/sinri/kele-web.git'
                    url = 'https://github.com/sinri/kele-web.git'
                }
            }
        }
    }
    repositories {

        maven {
            name = 'leqee-release'
            url = 'https://nexus.leqeegroup.com/repository/releases/'
            credentials {
                username = project.findProperty("leqeeNexusUsername")
                password = project.findProperty("leqeeNexusPassword")
            }
        }
        maven {
            name = 'leqee-snapshot'
            url = 'https://nexus.leqeegroup.com/repository/snapshots/'
            credentials {
                username = project.findProperty("leqeeNexusUsername")
                password = project.findProperty("leqeeNexusPassword")
            }
        }
//        maven {
//            name = 'OSSRH'
//            url = version.endsWith('SNAPSHOT')
//                    ? 'https://nexus.leqeegroup.com/repository/snapshots/'
//                    : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
//            credentials {
//                username = System.getenv("OSSRH_USERNAME")
//                password = System.getenv("OSSRH_PASSWORD")
//            }
//        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
